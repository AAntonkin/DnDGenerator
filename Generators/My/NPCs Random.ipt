use: srd\SRDTreasure.ipt
use: nbos\professions_rus.ipt
use: nbos\adventuringprofessions_rus.ipt
use: nbos\TownBuildings_rus.ipt
use: nbos\names\human_rus.ipt
use: nbos\names\elf_rus.ipt
use: nbos\names\dwarf_rus.ipt
use: nbos\names\towns_rus.ipt
use: nbos\names\orc_rus.ipt
use: nbos\Animals_rus.ipt
use: Generators\My\StatGenerator.ipt
use: nbos\Colors_rus.ipt
use: nbos\NPCs\NPCHair.ipt
use: nbos\NPCs\NPCEys.ipt
use: nbos\NPCs\NPCLips.ipt
use: nbos\NPCs\NPCReaction.ipt
use: nbos\NPCs\NPCCloses.ipt
use: nbos\RusRules\UpperLower.ipt
use: nbos\RusRules\Endings.ipt
use: nbos\Materials_rus.ipt
use: Generators\My\ArmaAndWeapons_rus.ipt
use: nbos\RusRules\GetSubStr.ipt
use: nbos\FantasyRaces.ipt

Header: Разные персонажи которых можно встретить на улице.

Set: anyGender=Любой

Table: RandomNPCPrint
[@RandomNPC][@15 =]<b> {$name} </b>[@15 =]													\n&
<b>Раса:</b>					[@34 TABS]{$NPC_Body}										\n&
<b>Пол:</b>						[@36 TABS]{$Sex}											\n&
<b>Алаймент:</b>				[@25 TABS][|Lawful|True|Chaotic] [|Good|Neutral|Evil]		\n&
<b>Профессия:</b>				[@22 TABS]{$Profession} (+{3+{$HD}+{1d5}}){$2ndProfession}	\n&
<b>Возраст:</b>					[@28 TABS]{$NPCAGE*$NPCAGEMOD} ({$NPC_AGE_LOOK})			\n&
<b>Геральдика:</b>				[@22 TABS]{$NPC_HeraldName}									\n&
<b>Запоминается:</b>																		\n&
								[@44 TABS] <b>Внешность:</b> {$NPC_LookHint}				\n&
								[@44 TABS] <b>Поведение:</b> {$LookHint}					\n&
<b>Характер:</b>	            [@27 TABS]{$NPC_REACTION100} 								\n&
<b>Реакция на РС:</b>			[@17 TABS]{$NPC_REACTION}									\n&
<b>Общий вид:</b>				[@23 TABS]Выглядит [#{$BtyMod} NPCGeneralLook]				\n&
--																							\n&
								{$NPC_LOOK}													\n&
--																							\n&
HD:[@24 TABS]					{$HD} ({$HP})												\n&
AC:[@24 TABS]					{$AC}														\n&
								[@ResultingStats]											\n&
[@8 =]

Table: RandomNPC
Set: GenderValue=[@Get_GenderValue]
Set: BtyMod_Promtp={$Prompt2}
Set: StatsFor=[when]{$Prompt4}[do]getStatFor{$Prompt4}[else]getStatForNPC[end]
Set: NPCSex=[@Gender]
Set: Size=0
Set: ArmorACBouns=0
Set: Race=[@NPCRace >> Proper]
Set: NPC_AGE_LOOK=[@NPCAgeLook]
Set: Profession=[@NPCProfession >> Proper]
Set: 2ndProfession=[@NPC2ndProfession >> Proper]
Set: Stats=[@{$StatsFor}]
Set: BtyMod2={$BtyMod}
Set: BtyModR={{$BtyMod_Promtp}+{$BtyMod}}
Set: BtyMod={if({$BtyModR}>3, 3, {if({$BtyModR}<-3, -3, {$BtyModR})})}
Set: Sex={$NPCSex}
//Set: NPC_GENERAL_LOOK=[#{$BtyMod} NPCGeneralLook]
Set: LookHint=[@NPCLookHint]
Set: HD={1d2}
Set: HP={{{$HD}-1}d4+3}
// Set: LooksHint_ending=xxx
Set: NPC_HeraldName_tmp=[@GetSubStr with {$name},2]
Set: NPC_HeraldName=[when]{$NPC_HeraldName_tmp}[do][|дом|семья|клан|род] {$NPC_HeraldName_tmp}[else](безродн{$UyAya})[end]
Set: NPC_LookHint=[@WhatWhereHint]
Set: NPC_Body=[@NPCBody]
Set: NPC_EYS=[@NPCEys with {$BtyMod}]
Set: NPC_HAIR=[@NPCHair with {$BtyMod}]
Set: NPC_LIPS=[@NPCLips with {$BtyMod}]
Set: NPC_CLOSES=[@NPCCloses with {$BtyMod}]
Set: NPC_FACE=[@NPCFace]
Set: NPC_LOOK=[@NPC_LOOK with {$BtyMod}]
Set: NPC_REACTION=[@UpperFirst with [@NPCReaction]]//[@NPCReaction] //[@UpperFirst with [@NPCReaction]]
Set: NPC_REACTION100=[@NPCReaction100]
Set: AC=[@ResultingAC]//{10+{$DexMod}+{$Size}+{$ArmorACBouns}} (10 + Dex ({$DexMod}) + Size({$Size} + Armor({$ArmorACBouns}))


Table: NPCProfession
10:[@Professions]
1:[@AdventuringProfessions]

Table: NPC2ndProfession
Set: _NPC2ndProfession_param1=[when]{$1}[do]{$1}[else][@NPCProfession][end]
Set: 1=
10:\z
1:, {_NPC2ndProfession_param1} (+{3+{$HD}+{1d4}})

Table: NPCRace
Set: 1=
5:Хуман[@Name==NPCHumanName]{NPCAGEMOD==1}//15
2:Дворф[@Name==MasterDwarfName]{NPCAGEMOD==3.5}//40
1:Эльф[@Name==MasterElfName]{NPCAGEMOD==5.3}//110
2:ПолуОрк[@Name==MasterOrcName]{NPCAGEMOD==0.9}
2:ПолуЭльф[@Name==NPCHumanName]{NPCAGEMOD==1.8}
2:Гном[@Name==NPCHumanName]{Size==1}{NPCAGEMOD==2.8}
2:Халфлинг[@Name==NPCHumanName]{Size==1}{NPCAGEMOD==1.4}

Table: NPCHumanName
[@MasterHumanName] [@MasterHumanName]

Table: NPC_LOOK
Set: _npclook_param1={$1}
Set: NPCSexLower=[@LowerFirst with {$NPCSex}]
Set: _npclook_NPCFaceHair={ if( {$GenderValue} = 1, '[@NPCFaceHair]', '')}
Set: _npclook_NPCFaceHairR=[when]{$_npclook_NPCFaceHair}[do], {$_npclook_NPCFaceHair}[end]
Set: 1=
// //
10:Перед вами {$NPCSexLower}, у {$NegoNeeL} {$NPC_EYS}, {$NPC_HAIR}. [@UpperFirst with {$NPC_FACE}], {$NPC_LIPS}{$_npclook_NPCFaceHairR}. [@UpperFirst with {$NPC_CLOSES}].
10:{$NPCSex},{$NPC_Body}, {$NPC_EYS}. [@UpperFirst with {$NPC_HAIR}]{$_npclook_NPCFaceHairR}, [@NPCLips]. [@UpperFirst with {$NPC_CLOSES}]
1:{$NPCSex}, горд{$UyAya}, статн{$UyAya}, выделяется пронзительный взор ясных глаз. Движения {$EgoEeL} кажутся быстрыми и порывистыми.
1:{Bty==12} {BtyMod==1} В простом дорожном плаще, накинутом на кожаную куртку, с тугим мешком за плечами; в правой руке, точно посох, {$OnOna} сжимает тяжелый и длинный бердыш. Грубые башмаки из воловьей кожи неутомимо шагают по покрытой прелым листом земле
1:{Bty==12} {BtyMod==1} {$NPCSex} кажется молод{$ImOy} — и в то же время никто не осмелился назвать бы {$EgoEeL} юн{$ImOy} . Мудрость веков читается в {$EgoEeL} взоре. Мудрость бессчетных веков, боль и надежда, горе и радость (И нет Смертного, которого этот взгляд оставил бы равнодушным). 
1:{Bty==10} {BtyMod==0} Перед вами {$NPCSexLower}, c медными волосами, правильными чертами лица, острым подбородком и прямым носом 
1:{Bty==10} {BtyMod==0} {$EgoEe} левую щёку рассекает свежий рубец, из раны сочится кровь. {$OnOna} говорит отрывисто, зло, размахивая руками
1:{Bty==10} {BtyMod==0} {$NPC_Body} в [@NPCCloses_Color with ой] одежде. На широком поясе слева висит длинный кинжал. В длинных светлых волосах видно изрядную долю седых прядей; глаза глубоко посажены, левую скулу рассекает длинный шрам
1:[@{$NPCSex} with {_npclook_param1}]
1:Лицо {$EgoEeL} в обрамлении длинных, схваченных кожаным ремешком на лбу [@NPCHair_Color with х] волос
1:{Bty==10} {BtyMod==0} Перед вашими глазами {$NPC_Body}, взгляд строгий, лицо же {$EgoEeL} бесстрастно
1:Мрачное лицо, брови сдвинулись, точно каменные выступы, а пальцы ожесточенно накручивают { if( {$GenderValue} = 1, 'завитки бороды', 'локоны волос')}
1:{ArmorACBouns==2}Крупный [@NPCHair_Color_Part_о]волосый воин средних лет, со свежим рубцом на лице
1:{Bty==12} {BtyMod==1} {$OnOna} {$NPC_Body}, [@NPCHair with {$BtyMod}], правильные и гармоничные черты лица.
// //
1:{Bty==12} {BtyMod==1} {$OnOna} одет{$HeShe_а} в [@NPCCloses_Color], утепленный овчиной, плащ с капюшоном, [|темно-[@Colors with ую]|[@Colors_Dark with ую]] &
бригантину,	под которой видно [@Colors] поддоспешник, ноги облачены в широкие тканевые штаны и утепленные кожаные сапоги.&
На поясе {$EgoEeL} висит обычный длинный одноручный меч, стилет в богатых [|темно-[@Colors]|[@Colors_Dark with ых]] ножнах,&
также видно туго набитый кошель и небольшая сумка, наполненная неизвестно чем.	Из-под капюшона выступает &
{$_npclook_NPCFaceHair} подбородок.
// //
1:Молод{$OyAya} {$ParenDevshka}{ if( {$GenderValue} = 1, '[@NPCFaceHairBeard]', '')}, в плотном светло-синем плаще и с таким же посохом в руках; на &
шее у {$NegoNeeL} висит кусочек темно-[@Colors_Dark with ого] отполированного дерева
// //
1:Грузн{$UyAya}, с большим животом, уже пожил{$OyAya} {$NPCSexLower} с двойным подбородком и красноватым носом; на груди у {$NegoNeeL} висит кусок светлой древесины. 
1:Перед вами стоит очень прям{$OyAya} и очень сух{$OyAya} [#{1d50+70} NPCAgeLookFromAge] с орлиным носом. Лицо {$EgoEeL} кажется коричневым; в руке {$OnOnaL} держит тонкую веточку, а на шее висит совершенно белый кусочек дерева. 
1:Стройное тело, неширокие плечи, изящные ладони. Лицо - не то мужское, не то женское, что-то среднее, без бороды и усов, со смягченными чертами и гладкой, лишенной морщин кожей.
1:Это {$NPCSexLower}, {$NPC_Body}, мощн{$UyAya}, в расцвете сил, с пышной гривой ис-синя-черных вьющихся волос. Прямой тонкий нос, выдающийся сильный подбородок, густые, сросшиеся на переносице брови; чуть полноватые, плотно сжатые губы.
//
1:{$EgoEe} нельзя назвать { if( {$GenderValue} = 1, 'писаным красавцем', 'писанной красавицей')}. Крючковатый нос явно переломлен по меньшей мере в трех местах, кожа лица стала бурой, подобно древесной коре. Она нависала пластами и складками, &
ее изрыли следы давно заживших рубцов и язв, брови почти отсутствуют, костистый подбородок выдается далеко вперед, и под седыми жидкими усами лежит прямой разруб рта. &
Однако ростом {$OnOnaL} по меньшей мере вдвое превосходит любого из {$Race}ов, на руках вздуваются могучие мышцы.{Str==14} {StrMod==2} 
//
1:[@UpperFirst with {$NPC_Body}], крепк{$IyAya} и кряжист{$UyAya}, беловолос{$UyAya} и белобров{$UyAya}, с хищно загнутым вниз носом, напоминающим клюв сокола или орла.
1:Могуч{$IyAya} {$NPCSexLower} с пылающими глазами, лишенными зрачков, - кажется, внутри черепа горит неугасимый огонь.
1:Нахмуренн{$UyAya}, с опаленными огнем курчавящимися { if( {$GenderValue} = 1, 'усами и недлинной бородкой', 'волосами')}, не уступающ{$IyAya} шириной плеч любому из Дворфов, но с более грубыми очертаниями чрезмерно вздутых мускулов; 
1:{Bty==12} {BtyMod==1}{$NPC_Body=="-"}Это {$NPCSexLower}, {$NPC_Body} в плотно облегающей тело [@NPCCloses_Color with ой] одежде, на украшенном изумрудами поясе висит длинный меч, широкий и толстый. 
// // NPCBody
1:{$OnOna} облачен{$HeShe_а} в изукрашенный шитый пояс, что перетягивает {$EgoEeL} простой коричневый кафтан, очень похожий на одежду горожанина со средним достатком. В руке толстый отполированный дубовый посох; &
коричневатая мощная рука с узловатыми пальцами, сжимает резную рукоять и кажется продолжением отполированного древесного корня, из которого сделан посох.
// //

Table: Мужчина
Set: 1=
Мужчина высок, широкоплеч, лицо обрамляет полуседая борода, серебро виднеется и на висках, но глаза ясны, а руки, кажется, могут запросто гнуть подковы [@Profession==ProfessionsStr]{Str==14} {StrMod==2}
Голова перевязана, заросшее рыжей бородой лицо украшает внушительный синяк, костяшки кулаков содраны
{ArmorACBouns==4}{Profession=="стражник"} Рослый бородатый часовой, смуглый и курчавоволосый
У него круглое красное лицо под низкими кожаными складками странного подобия боевого подшлемника
Спокойный, прямой и строгий, облаченный в лучшие одежды {Bty==13} {BtyMod==1}
Мощный седобородый, его лицо темно-багровое (какое бывает от жара топок) {Profession=="кузнец"}
(РОГВОЛД)Высокий седой человек, уже очень немолодой, но сухой, подтянутый; на загорелом лице под густыми седыми бровями выделяются [@NPCEys_Color] глаза такой&
	редкостной чистоты, что можно невольно залюбоваться. Гладкая кожа обтягивает чуть выступающие скулы,&
	от крыльев носа к уголкам рта пролегли глубокие складки, мелкая сеть морщинок залегла в уголках глаз; низ лица скрывает аккуратная белоснежная&
	бородка, ровной лентой тянувшаяся от одного уха до другого. На нем простая коричневая куртка и высокие кожаные сапоги; на поясе, с каждого&
	боку, висит два коротких ножа. Длинные волосы перехвачены кожаным шнурком, чтобы не закрывали глаз.
(Торин Эльф. Клинок) Плотный, широкоплечий, румяное лицо обрамляет окладистая борода, нос картошкой... За узорным широким поясом - [|тяжелый топор|[@Weapon_OnMyBelt]], за спиной [|приторочена крика|[@Weapon_OnMyBack]].
{Str==14} {StrMod==2} {Con==14} {ConMod==2} {Bty==14} {BtyMod==2} Огромный детина в черном облегающем костюме. Первое, что бросалось в глаза при взгляде на него, так это кожа необычного, густого темно-серого цвета.&
	Невероятно красивые глаза цвета янтаря, в обрамлении длинных черных ресниц, яркими пятнами выделявшиеся на фоне серого лица. За такие ресницы любая бы девица удавилась от зависти! 
{NPCAGE==75} {NPC_AGE_LOOK=="старик"} {LookHint=="стройный"} Высокая, одетая в [@Colors with ое] фигура старика с длинным белым посохом в левой руке
// //
Ражий детина с густой курчавой бородой, с головы до ног закованный в отменную сталь пластинчатых доспехов
// //
Широкоплечий бородач с окладистой бородой мало что не до пояса приветственно махнул рукой
// //
{Cha==12} {ChaMod==1} {Bty==12} {BtyMod==1} Громким голосом, крупный, эффектный, красиво седеющий. У него проницательный взгляд из-под густых бровей, чеканный профиль со слегка выпяченной нижней губой, будто он совершенно по-детски обращается к миру с вопросом: «Почему все не так, как я хочу?»

Table: Женщина
Set: _npcfemale_param1={$1}
Set: 1=
Она выглядит [#2 NPCGeneralLook], &
[@NPCHair with 2], &
[@NPCCloses_GeneralЖенщина], &
в талии стянут{$NPCCloses_HeSheEnding} бесчисленным количеством выточек и лиловых лент. {Bty==14} {BtyMod==2}
// //
{NPCAGE==75} {NPC_AGE_LOOK=="старуха"} Высокая, одетая в [@Colors with ое] фигура старухи с длинным белым посохом в левой руке
// // 
Перед Вашими глазами предстало, испещренное морщинами, лицо глубокой старухи. Редкие седые волосы висят спутанными прядями, уголки потрескавшихся&
губ неестественно опущены вниз, обнажая нижние зубы, кривыми осколками выглядывающие из-под губы. Но самым страшным являются глаза.&
Абсолютно выцветшие, ничего не выражающие глаза, уставились на Вас.{NPCAGE==79}{NPC_AGE_LOOK=="старуха"} 
// //
(Сигрлин) Ее стройное тело облегает длинное [@NPCCloses_Color with ое] платье, дивные волосы ниспадают волной на плечи и спину; она не имеет при себе никакого оружия &
- кажется, ее оторвали от праздничного пиршественного стола. Красота этой женщины завораживает, ее глаза, полные печального укора, глубокие и понимающие несут в себе умиротворение. 	
// //
Спокойная, осанистая, широкобедрая женщина, чьи волосы схожи со львиной гривой.
Эта женщина на вид {$NPC_AGE_LOOK}, легкая и подвижная, словно птица, она пританцовывает на месте, тихо что то напевая.
{NPCAGE==16} {NPC_AGE_LOOK=="Девушка"} {LookHint=="стройная"} Тоненькая, словно тростинка, с веселой копной солнечно-светлых, выгоревших волос, состриженных коротко с огромными изумрудными глазами.
Смуглая кожа, тонкие руки с позвякивающими тонкими браслетами, горькая морщинка на лбу рассказывает о том, как когда-то была упущена мечта. Короткое платьице, свободно падающие темные локоны, открытая улыбка.

Table: NPCFaceHair
10:[@NPCFaceHairBeard]
10:[@NPCFaceHairMustach]
5:[@NPCFaceHairMustach][@NPCFaceHairBeard]
5:лицо обрамляет полуседая борода
	
Table: NPCFaceHairBeard
Set: _npcfhb_param1={$1}
Set: 1=
5:\z
чисто выбритый
заросший короткой щетиной
густо обросший щетиной
бородатый
с легкой бородкой

Table: NPCFaceHairMustach
Set: _npcfhm_param1={$1}
Set: 1=
5:\z
мелкие усики
густые, вислые усы

Table: NPCFace
невозмутимое и непроницаемое лицо
[|вытянутый|квадратный|узкий|широкий] [|подбородок|лоб]

Table: NPCFace_If_Wounded
[when]{$Bty}<11[do][@NPCFace_Wounded][else]\z[end]

Table: NPCFace_Wounded
лицо - болезненно-бледное
лоб и щеки отмечены шрамами
лицо украшает внушительный синяк
левую щёку рассекает свежий рубец, из раны сочится кровь
[|заметен|выделяется] длинный багровый рубец — довольно свежий, ему еще предстоит стать белым шрамом
левую скулу рассекает длинный шрам
через весь лоб, наискось, от виска до носа, тянется чёрная запекшаяся рана

Table: NPCNose
на носу очки в толстой роговой оправе
в носу пирсинг 
аккуратненький прямой носик
немного курнос{$IyAya}

Table: NPCBody
{$Race}, высокого (по своим меркам) роста 
{$Race}, невысокого (по своим меркам) роста
{$Race}, среднего роста
{$Race}, обычного роста
{$Race}, огромного (по своим меркам) роста

Table: NPCGeneralLook
type: dictionary
-3:[|ужсающе|пугающе|черезвычайно] плохо
-2:[|плохо|пугающе|страшно]
-1:плохо
1:хорошо
2:[|[|очень|черезвычайно] хорошо| прекрасно| очаровательно]
3:[|восхитительно|сногсшибательно|умопомрачительно]
default: по обычному

Table: NPCAgeLookFromAge
0-9:ребенок{NPCAGE==(1d10-1)*NPCAGEMOD}	//0-9
10-15:{ if( {$GenderValue} = 1, 'мальчик', 'девочка')}{NPCAGE==(9+1d6)*NPCAGEMOD}	//10-15
16-23:{ if( {$GenderValue} = 1, 'юноша', 'девушка')}{NPCAGE==(15+1d8)*NPCAGEMOD}	//15-23
23-39:средних лет{NPCAGE==(23+1d10+1d6)*NPCAGEMOD}									//23-39
40-58:{ if( {$GenderValue} = 1, 'зрелый', 'зрелая')}{NPCAGE==(38+2d10)*NPCAGEMOD}	//40-58
59-69:{ if( {$GenderValue} = 1, 'пожилой', 'пожилая')}{NPCAGE==(59+d10)*NPCAGEMOD}	//59-69
70-120:{ if( {$GenderValue} = 1, 'старик', 'старуха')}{NPCAGE==(59+d10)*NPCAGEMOD}	//70-120

Table: NPCAgeLook
1:ребенок{NPCAGE==(1d10-1)}	//0-9
10:{ if( {$GenderValue} = 1, 'мальчик', 'девочка')}{NPCAGE==(9+1d6)}	//10-15
30:{ if( {$GenderValue} = 1, 'юноша', 'девушка')}{NPCAGE==(15+1d8)}	//15-23
100:средних лет{NPCAGE==(23+1d10+1d6)}									//23-39
100:{ if( {$GenderValue} = 1, 'зрелый', 'зрелая')}{NPCAGE==(38+2d10)}	//40-58
100:{ if( {$GenderValue} = 1, 'пожилой', 'пожилая')}{NPCAGE==(59+d10)}	//59-69
40:{ if( {$GenderValue} = 1, 'старик', 'старуха')}{NPCAGE==(59+d10)}	//70-120

Table: NPCHeraldName


Table: ResultingAC
{10+{$DexMod}+{$Size}+{$ArmorACBouns}} (10 + Dex ({$DexMod}) + Size({$Size} + Armor({$ArmorACBouns}))

Table: WhatWhereHint
[@WhereHint] видно [@WhatHint]
[@NPCLookHintBody]

Table: WhatHint
[@SizeHint with ую] [|уродливую||||||] [|волосатую|розовую|черную|светлую|||||||] [|родинку|бородавку|экзему]
[|не|||] [|большой|маленький][|, глубокий|, еле заметный|, старый||] [|, уродливый|||] [|шрам|ожог|синяк] [|размером [@SizeHint2]||]
[@SizeHint with ое] [|, уродливое|||||||||] [|, розовое|, черное|||||||] [|, темное||, светлое|||] родимое пятно

Table: SizeHint
Set: _sh_param1={$1}
Set: 1=
[|не|||] [|больш|маленьк|огромн]{$_sh_param1}

Table: SizeHint2
с кулак
с ноготь
с булавку
с монету

Table: WhereHint
на [|левой|правой] [|щеке|брови|части шеи|кисти руки]
[|под|над] носом
[|под|над] [|левым|правым] глазом

Table: HardMaterial
Set: _hm_p1={$1}
Set: 1=
металический ([#Metal Material_This with {$_hm_p1}])
деревянный ([#Wood Material_This with {$_hm_p1}])

Table: NPCLookHintBody
Set: someHardMetal=[@HardMaterial with ый]
Во рту [|отсутствует|гнилой|{$someHardMetal}] передний зуб
На руке отсутствует [|[|большой палец|указательный палец|безимянный палец|средний палец|мизинец]|несколько пальцев ({1d3+1})]
На руке [|гнилой|{$someHardMetal}] [|большой палец|указательный палец|безимянный палец|средний палец|мизинец] 
Вместо [|пальца|кисти] [|шило|обрубок|крючок|{$someHardMetal} протез]] 
Имеет очень неприятный запах изо рта
Имеет сильный специфический запах тела (зависит от профессии)
Имеет приятный запах (парфум) {Bty==12} {BtyMod==1}
Имеет грязное, неопрятное тело {Bty==9} {BtyMod==-1}
Имеет чистое, ухоженное тело {Bty==14} {BtyMod==2}

Table: NPCLookHint
Обильно потеет
Очень сильно жмет руку
Имеет необычный цвет глаз
Постоянно заходится удушающим кашелем
Чихает и сморкает носом
Имеет очень низкий голос
Имеет очень высокий голос
Проглатывает части слов
[|Не приятно|Забавно] [|шепелявит|заикается]
Четко объясняет. "Значит так..."
Говорит очень громко
Постоянно шепчет
Постоянно переспрашивает (глуховат{$HeShe_а}).. А? Что? Ааааа...
Имеет тату [@WhereHint]
Имеет родинку [@WhereHint]
Кожа покрыта пятнами
//Плешь на голове
//Особенно длинные волосы
Постоянно нервно подергивается
Имеет необычный цвет волос
Имеет странную походку, страдает хромотой [|ходит с [|палочкой|костылем]|]
Имеет странное украшение
Носит яркую или диковинную одежду
Имеет очень скромную одежду {Bty==10} {BtyMod==0}
Имеет слишком нарядная одежду {Bty==12} {BtyMod==1}
Нервно подергивается глаз
Постоянно теребит свои руки
Постоянно присвистывает
Все время напевает себе под нос
Все время подкидывает монету
Имеет хорошую осанку, держит спину ровно 
Спина выгнута слегка назад
Имеет худощавое телосложение, худ{$OyAya} как тросточка {Con==9} {ConMod==-1}
Имеет плотное телосложение, кругл{$UyAya} как колобок {Con==14} {ConMod==2}
Имеет видимые [|раны|язвы] на теле
Имеет ярко выраженное косоглазие
Держится на расстоянии от собеседников
Постоянно, что то жует
Имеет примечательный нос, [|с горбинкой|слегка вздернутый|как у орла|пятачком]
Очень самовлюбленн{$UyAya}
Очень угодлив{$UyAya}, раболепн{$UyAya}
Прибывает в постоянном сонном состоянии
Обожает книги или попросту - книжный червь
Довольно наблюдательн{$UyAya}
Не очень наблюдательн{$UyAya}, рассеянн{$UyAya}
Чрезмерно и постоянно критикует [|NPC|PC|погоду|правительство|[@FantasyRacesi with ов]]
Очень сильно любит [|рыбалку|охоту|игры|животных] (рыбалку,охоту,игры,животных и т.д.)
Является заядлым коллекционером [|книг|военных трофеев|монет|оружия|украшений] (книги, трофеи, монеты, оружие и т.д.)
Является чрезвычайно скупым человеком - [|скрягой|жмотом|скупердяем]
Очень расточител{$EnNa}
Очень пессимистич{$EnNa}
Очень оптимистич{$EnNa}
Любит выпить - пьяница
Постоянно курит [|отвратительный|восхитительный|обычный|приятный] табак [||на основе какой то травы|на основе каких то грибов]], используя &
	[||не] [|обычную трубку|трубку с длинным мундштуком|трубку с необычным стаммелем, выглядит как [@NormalAnimals]]
Постоянно Жует [|отвратительный|восхитительный|обычный|приятный] табак [||на основе какой то травы|на основе каких то грибов]]
Очень негативно относится к алкоголю и тем кто его употребляет
Имеет хорошее воспитания, очень воспитанн{$UyAya}
Не имеет воспитания и ведет себя неприлично
Припрыгивает при ходьбе
Имеет индивидуалистический (ставящий интересы отдельной личности выше всего) характер
Имеет конформистский (пассивное принятие существующего порядка) характер
Очень горд{$UyAya}
Сильно любознательн{$UyAya}
Имеет [|довольно жестокий|капризный|энергичный|тщеславный|властный|отчужденный|вспыльчивый|спокойный|невротический|завистливый|бесстрашный(храбрый)|трусливый|неосторожный] характер
Всегда говорит правду и ненавидит ложь
Постоянно лжет
Чрезмерно ленив{$HeShe_а}
[|Благоговейно|Благочестиво] относится к окружающим
Явно выражает свое безбожие и не религиозность
Имеет сильные, [|негативные|позитивные|агрессивные] взгляды на [|политику|мораль|религию|богатство|образованность]
Использует цветочную/приукрашенную речь или длинные слова...
Постоянно использует одни и те же фразы
Имеет сексуальные предрассудки ([|{$Gay}|би|животные|фетиш(одежда)|к {$Race}ам]) 
Имеет расистские предрассудки к {$Race}ам 
Имеет предрассудки по поводу [|политики|морали|религии|богатства|образованности]
Очень восторгается магией
Сильно не доверяет магии
Предпочитает членов одного класса ([@AdventuringProfessions]) над всеми остальными
Постоянно подшучивает над окружающими
Не имеет чувства юмора

Table: Gender
Set: NPCSex				={ if( {$GenderValue} = 1, 'Мужчина', 'Женщина')}
Set: ParenDevshka		={ if( {$GenderValue} = 1, 'парень', 'девушка')}
Set: OnOna				={ if( {$GenderValue} = 1, 'Он', 'Она')}
Set: OnOnaL				={ if( {$GenderValue} = 1, 'он', 'она')}
Set: NegoNee			={ if( {$GenderValue} = 1, 'Него', 'Нее')}
Set: NegoNeeL			={ if( {$GenderValue} = 1, 'него', 'нее')}
Set: EgoEe				={ if( {$GenderValue} = 1, 'Его', 'Ее')}
Set: EgoEeL				={ if( {$GenderValue} = 1, 'его', 'ее')}
Set: NemNeyL			={ if( {$GenderValue} = 1, 'нем', 'ней')}
Set: MF					={ if( {$GenderValue} = 1, 'MALE', 'FEMALE')}
Set: HeShe_а			={ if( {$GenderValue} = 1, '', 'а')}
Set: IyAya				={ if( {$GenderValue} = 1, 'ий', 'ая')}
Set: UyAya				={ if( {$GenderValue} = 1, 'ый', 'ая')}
Set: ImOy				={ if( {$GenderValue} = 1, 'ым', 'ой')}
Set: OyAya				={ if( {$GenderValue} = 1, 'ой', 'ая')}
Set: EnNa				={ if( {$GenderValue} = 1, 'ен', 'на')}
Set: EmuEy				={ if( {$GenderValue} = 1, 'ему', 'ей')}
Set: Gay				={ if( {$GenderValue} = 1, 'гей', 'лесбиянка')}
{$NPCSex}

Table: Get_GenderValue
Set: _get_prom_sexM =[when]{$Prompt1} = Мужчина[do]Мужчина[else][when]{$Prompt1} = Женщина[do]Женщина[else]Любой[end][end]
Set: _get_genval_f=[when]{$1}[do]{$1}[else]{$_get_prom_sexM}[end]
Set: _get_genval_param1=[#{$_get_genval_f} Get_GenderS]
Set: 1=
Set: GenderOne=[when]{$_get_genval_param1} = 1[do]Мужчина[else]Женщина[end]
{$_get_genval_param1}

Table: Get_GenderS
type:dictionary
Мужчина:1
Женщина:2
default:[|1|2]


Table: TABS
&nbsp
Table: -
-
Table: =
=

