#
# This material is Open Game Content, and is licensed for public use under the terms of the Open Game License v1.0a.
#
Header: Random treasure for the selected Challenge Rating, based on the OGL treasure tables.  Use the prompt at the bottom left of the window to select CR.
Footer: <br />This material is Open Game Content, and is licensed for public use under the terms of the Open Game License v1.0a.

Prompt: Choose a CR (1-30) {Random|1|2|3|4|5|6|7|8|9|10|11|12|13|14|15|16|17|18|19|20|21|22|23|24|25|26|27|28|29|30}1

Use:common/srd/SRDTreasure.ipt

Table: TreasureProxy
[when]{$prompt1}=Random[do]{!CRValue=={1d30}}[else]{!CRValue=={$prompt1}}[end][@TreasureByCR]

Table: TreasureByCR
Set: CoinsTable=CR{$CRValue}Coins
Set: GoodsTable=CR{$CRValue}Goods
Set: ItemsTable=CR{$CRValue}Items
Set: CoinsCount=0
Set: GoodsCount=0
Set: ItemsCount=0
Set: CoinsValue=0
Set: GoodsValue=0
Set: ItemsValue=0
CR {$CRValue} Treasure...<ul><li>Coins: [@{$CoinsTable}]</li><BR /><BR /><li>Goods:<ul>[@{$GoodsTable}]</ul><BR />Total Value for {$GoodsCount} Good[when not]{$GoodsCount}=1[do]s[end]: [@GoldCheck with {$GoodsValue}]</li><BR /><BR /><li>Items:<ul>[@{$ItemsTable}]</ul><BR />Total Value for {$ItemsCount} Item[when not]{$ItemsCount}=1[do]s[end]: [@GoldCheck with {$ItemsValue}]</li></ul>Total CR {$CRValue} Treasure Value: [@GoldCheck with {!TotalValue={$CoinsValue}+{$GoodsValue}+{$ItemsValue}}]

Table: GoldCheck
Set: SubmittedValue={$1}
Set: SubmittedFloor={! floor({$SubmittedValue})}
Set: SilverCheck={! ({$SubmittedValue}-{$SubmittedFloor})*10 }
[@AddCommas with {$SubmittedFloor}] gp[when]{$SilverCheck}>0[do] {$SilverCheck} sp[end]

Table: AddCommas
Set: NewValue={$1}
Set: CurrentLength=[{$NewValue} >> Length]
Set: NewLength={! {$CurrentLength}-1}
Set: NextCharacter=[{$NewValue} >> Substr 1]
Set: CeilCheck={! ceil({$NewLength}/3)}
Set: NormalCheck={! {$NewLength}/3}
{$NextCharacter}[when]{$NewLength}=0[do]{!CeilCheck=={$NormalCheck}+1}[end][when]{$CeilCheck}={$NormalCheck}[do],[end][when]{$NewLength}>0[do][@AddCommas with [{$NewValue} >> Substr 2 {$NewLength}]][end]

